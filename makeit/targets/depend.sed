#n

# Copyright 1995-2003 DUMPLETON SOFTWARE CONSULTING PTY LIMITED

# Convert DOS backslash to normal slash. Note that this is done to all
# lines, same with the subsequent substitution.

s%\\%/%g

# Replace multiple consecutive occurences of slash with a single slash.

s%//%/%g

# Massage the lines generated by the preprocessor which are interested in.

s%^ *\# *[1-9][0-9]* "\(.*\)".*$%#TARGET : \1%
s%^ *\#line [1-9][0-9]* "\(.*\)".*$%#TARGET : \1%

# Remove the lines which have a drive specifier in them.

/^#TARGET : [a-zA-Z]:.*$/d

# Remove strange lines on MAC OS X such as <built-in> and <command line>.

/^#TARGET : [<].*$/d

# Print out those finally left which we are interested in.

s%^#\(TARGET.*\)$%\1%p
